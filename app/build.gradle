apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {

    // 调试
    boolean devBuildProperty = rootProject.hasProperty('devBuild')
    if (devBuildProperty) {
        splits.abi.enable = false
        splits.density.enable = false
        // 禁用PNG压缩
        aaptOptions.cruncherEnabled = false
    }

    compileSdkVersion rootProject.ext.android["compileSdkVersion"]

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        if (devBuildProperty) {
            // 调试，指定编译打包的资源文件
            resConfigs("zh", "xxxhdpi")
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        //必须，告知RxHttp你依赖的okhttp版本，目前已适配 v3.12.0 - v4.7.2版本  (v4.3.0除外)
                        rxhttp_okhttp: '4.7.2',
                        //使用asXxx方法时必须，告知RxHttp你依赖的rxjava版本，可传入rxjava2、rxjava3
                        rxhttp_rxjava: 'rxjava2',
                        rxhttp_package: 'rxhttp'   //非必须，指定RxHttp相关类的生成路径，即包名
                ]
            }
        }

        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "LOG_DEBUG", "true")
            buildConfigField("String", "LOG_TAG", "\"Meet-Kotlin\"")
        }
        release {
            buildConfigField("boolean", "LOG_DEBUG", "false")
            buildConfigField("String", "LOG_TAG", "\"Meet-Kotlin\"")

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Bmob中需要使用HttpClient
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':base')
    implementation project(':RongIMLib')

    testImplementation rootProject.ext.deps["junit"]
    androidTestImplementation rootProject.ext.deps["androidx.test.ext"]
    androidTestImplementation rootProject.ext.deps["androidx.test.espresso"]

    implementation rootProject.ext.deps["kotlin-reflect"]
    implementation rootProject.ext.deps["3dtagcloud"]
//    implementation rootProject.ext.deps["rong-cloud-sdk"]

    // rxhttp
    implementation 'com.ljx.rxhttp:rxhttp:2.2.8'
    kapt 'com.ljx.rxhttp:rxhttp-compiler:2.2.8'
    implementation 'com.ljx.rxlife:rxlife-coroutine:2.0.0'
    implementation 'com.ljx.rxlife2:rxlife-rxjava:2.0.0'
}
